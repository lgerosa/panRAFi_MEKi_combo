begin model
begin parameters
  M_vol      0.001
  C_vol      1
  RasGTP_0   0
  Raf_0      0.01
  I_0        0
  kr_GR      10
  kf_GR      10
  kr_RR      10
  kf_RR      1
  phi_RR     1
  kr_RI      0.10000000000000001
  kf_RI      1
  phi_RI     1
  f          1
  g          1
  Gf_RR      ln(kr_RR/kf_RR)
  Ea0_RR     -ln(kf_RR) - phi_RR*ln(kr_RR/kf_RR)
  Gf_RI      ln(kr_RI/kf_RI)
  Ea0_RI     -ln(kf_RI) - phi_RI*ln(kr_RI/kf_RI)
  Gf_f       ln(f)
  Gf_g       ln(g)
  Gf_RRI     Gf_f
  Gf_IRRI    Gf_f + Gf_g
end parameters

begin compartments
  M  2  M_vol  
  C  3  C_vol  M
end compartments

begin molecule types
  Ras(raf,state~GTP)
  Raf(ras,raf,i)
  I(raf)
end molecule types

begin observables
  Molecules Rtot_obs               Raf()
  Molecules Itot_obs               I()
  Molecules R_BRAFmut_active_obs   Raf(i)
  Molecules R_RAFwt_active_obs     Raf(raf!1,i).Raf(raf!1)
  Species R_obs                  Raf(raf,i)
  Species RR_obs                 Raf(raf!1,i).Raf(raf!1,i)
  Species RRI_obs                Raf(raf!1,i).Raf(raf!1,i!2).I(raf!2)
  Species IRRI_obs               I(raf!2).Raf(raf!1,i!2).Raf(raf!1,i!3).I(raf!3)
end observables

begin species
  Ras(raf,state~GTP)@M   RasGTP_0
  Raf(ras,raf,i)@C       Raf_0
  I(raf)@C               I_0
end species

begin energy patterns
  ep_RR:   Raf(raf!1).Raf(raf!1)    Gf_RR
  ep_RI:   Raf(i!1).I(raf!1)    Gf_RI
  ep_RRI:  Raf(raf!1,i).Raf(raf!1,i!2).I(raf!2)    Gf_RRI
  ep_IRRI:  I(raf!2).Raf(raf!1,i!2).Raf(raf!1,i!3).I(raf!3)    Gf_IRRI
end energy patterns

begin reaction rules
  GR:  Ras(raf,state~GTP) + Raf(ras) <-> Ras(raf!1,state~GTP).Raf(ras!1)    kf_GR, kr_GR
  RR:  Raf(raf) + Raf(raf) <-> Raf(raf!1).Raf(raf!1)    Arrhenius(phi_RR, Ea0_RR)
  RI:  Raf(i) + I(raf) <-> Raf(i!1).I(raf!1)    Arrhenius(phi_RI, Ea0_RI)
end reaction rules

end model
